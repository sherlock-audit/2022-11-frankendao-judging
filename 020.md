0xbepresent

medium

# VotingPower manipulation via Governance.sol::verifyProposal()

## Summary

It is possible to increase the proposer voting power via the ```Governance.sol::verifyProposal()``` function.

## Vulnerability Detail

The ```verifyProposal()``` function was created to able to approve the community proposals, so the admin can call  the function and the proposal will pass to a ```verified``` state. The problem here is that the admin can call accidentally/intentionally the function multiple times causing and incremention of proposer voting power and total community proposals.

## Impact

The admin can accidentally/intentionally increment the proposer voting power and change the total community proposals. 

- The proposer can cast a vote to a proposal with a high weight.
- Malicious proposals can be approved 
- The total community proposals (```totalCommunityScoreData.proposalsCreated```) storages an invalid number of real proposals.

I wrote a test which helps to see the proposer voting power incremention:

```solidity
// CommunityVotingPower.t.sol
// forge test -m "testGovState__0xbepresent__VerifiedProposalVotingPowerManipulation" -vvv
function testGovState__0xbepresent__VerifiedProposalVotingPowerManipulation() public {
    // VotingPower manipulation via Governance.sol::verifyProposal()
    // The verifyProposal() does not check the proposal verify.
    // 1. Create a proposal
    // 2. Verify the proposal (first time)
    // 3. Verify the proposal (second time)
    // 4. Verify the proposal (third time)
    uint proposalId;
    address proposer;
    uint64 totalProposalsCreatedBeforeVerify;
    uint64 totalProposalsCreatedAfterVerify;
    uint proposerPowerBeforeProposalVerification;
    uint proposerPowerAfterProposalVerification;
    // check the totalCommunityScoreData
    (,totalProposalsCreatedBeforeVerify,) = gov.totalCommunityScoreData();

    //
    // 1. Create a proposal
    //
    proposalId = _createProposal();
    (,proposer,) = gov.getProposalData(proposalId);
    proposerPowerBeforeProposalVerification = staking.getCommunityVotingPower(proposer);
    console.log("Total community proposals before verify:",totalProposalsCreatedBeforeVerify,"Proposer voting power",proposerPowerBeforeProposalVerification);

    //
    // 2. Verify the proposal (first time)
    //
    vm.prank(COUNCIL_MULTISIG);
    gov.verifyProposal(proposalId);
    // check the totalCommunityScoreData
    (,totalProposalsCreatedAfterVerify,) = gov.totalCommunityScoreData();
    proposerPowerAfterProposalVerification = staking.getCommunityVotingPower(proposer);
    console.log("Total community proposals after verify the same id verifyProposal():",totalProposalsCreatedAfterVerify,"Proposer voting power",proposerPowerAfterProposalVerification);
    assertEq(totalProposalsCreatedBeforeVerify + 1, totalProposalsCreatedAfterVerify);
    assertEq(proposerPowerBeforeProposalVerification + 2, proposerPowerAfterProposalVerification);

    //
    // 3. Verify the proposal (second time)
    //
    vm.prank(COUNCIL_MULTISIG);
    gov.verifyProposal(proposalId);
    // check the totalCommunityScoreData
    (,totalProposalsCreatedAfterVerify,) = gov.totalCommunityScoreData();
    proposerPowerAfterProposalVerification = staking.getCommunityVotingPower(proposer);
    console.log("Total community proposals after verify the same id verifyProposal():",totalProposalsCreatedAfterVerify,"Proposer voting power",proposerPowerAfterProposalVerification);
    assertEq(totalProposalsCreatedBeforeVerify + 2, totalProposalsCreatedAfterVerify);
    assertEq(proposerPowerBeforeProposalVerification + 4, proposerPowerAfterProposalVerification);

    //
    // 4. Verify the proposal (third time)
    //
    vm.prank(COUNCIL_MULTISIG);
    gov.verifyProposal(proposalId);
    // check the totalCommunityScoreData
    (,totalProposalsCreatedAfterVerify,) = gov.totalCommunityScoreData();
    proposerPowerAfterProposalVerification = staking.getCommunityVotingPower(proposer);
    console.log("Total community proposals after verify the same id verifyProposal():",totalProposalsCreatedAfterVerify,"Proposer voting power",proposerPowerAfterProposalVerification);
    assertEq(totalProposalsCreatedBeforeVerify + 3, totalProposalsCreatedAfterVerify);
    assertEq(proposerPowerBeforeProposalVerification + 6, proposerPowerAfterProposalVerification);
}
```

## Code Snippet

[Governance.sol::verifyProposal()](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L445)

```solidity
function verifyProposal(uint _proposalId) external onlyAdmins {
    // Can only verify proposals that are currently in the Pending state
    if (state(_proposalId) != ProposalState.Pending) revert InvalidStatus();

    Proposal storage proposal = proposals[_proposalId];
    proposal.verified = true;

    // If a proposal was valid, we are ready to award the community voting power bonuses to the proposer
    ++userCommunityScoreData[proposal.proposer].proposalsCreated;
    
    // We don't need to check whether the proposer is accruing community voting power because
    // they needed that voting power to propose, and once they have an Active Proposal, their
    // tokens are locked from delegating and unstaking.
    ++totalCommunityScoreData.proposalsCreated;
}
```

## Tool used

VisualStudio/Foundry

## Recommendation

Add a ```verified``` validation in the ```verifyProposal()``` function:

```solidity
if (proposal.verified) revert AlreadyVerified();
```