hansfriese

medium

# A proposal can be verified several times and `CommunityScoreData.proposalsCreated` would be tracked wrongly.

## Summary
A proposal can be verified several times and `CommunityScoreData.proposalsCreated` would be tracked wrongly.

## Vulnerability Detail
A pending order can be verified using [verifyProposal()](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L445-L459).

```solidity
    function verifyProposal(uint _proposalId) external onlyAdmins { //@audit verify again
        // Can only verify proposals that are currently in the Pending state
        if (state(_proposalId) != ProposalState.Pending) revert InvalidStatus();

        Proposal storage proposal = proposals[_proposalId];
        proposal.verified = true;

        // If a proposal was valid, we are ready to award the community voting power bonuses to the proposer
        ++userCommunityScoreData[proposal.proposer].proposalsCreated;
        
        // We don't need to check whether the proposer is accruing community voting power because
        // they needed that voting power to propose, and once they have an Active Proposal, their
        // tokens are locked from delegating and unstaking.
        ++totalCommunityScoreData.proposalsCreated;
    }
```

And it increases the `CommunityScoreData.proposalsCreated` inside the function.

But when we check the proposal state [here](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L270-L284), the state is still pending when `block.timestamp < proposal.startTime` even if it's verified already.

```solidity
function state(uint256 _proposalId) public view returns (ProposalState) {
    if (_proposalId > proposalCount) revert InvalidId();
    Proposal storage proposal = proposals[_proposalId];

    // If the proposal has been vetoed, it should always return Vetoed.
    if (proposal.vetoed) {
        return ProposalState.Vetoed;

    // If the proposal isn't verified by the time it ends, it's Canceled.
    } else if (proposal.canceled || (!proposal.verified && block.timestamp > proposal.endTime)) {
        return ProposalState.Canceled;

    // If it's unverified at any time before end time, or if it is verified but is before start time, it's Pending.
    }  else if (block.timestamp < proposal.startTime || !proposal.verified) {
        return ProposalState.Pending;
```

So if `verifyProposal()` is called again for the already verified proposal, `CommunityScoreData.proposalsCreated` will be increased again.

I know `verifyProposal()` is an `onlyAdmins` function and it's in case the admin calls the function twice for the same proposal by fault.

## Impact
`CommunityScoreData.proposalsCreated` would be increased wrongly when `verifyProposal()` is called several times for the same proposal.

## Code Snippet
https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L445-L459

## Tool used
Manual Review

## Recommendation
We should check if the proposal is already verified [here](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L449).


```solidity
    Proposal storage proposal = proposals[_proposalId];
    require(!proposal.verified, "verified already"); //+++++++++++++++++++++
    proposal.verified = true;
```