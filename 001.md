Ruhum

high

# User can vote multiple times by delegating their voting power to different addresses

## Summary
Users can vote, delegate and then vote again. They are able to vote as many times as they want. Because of the gas refunds, for voting, the user only has to pay for the delegation txs.

## Vulnerability Detail
User can delegate their tokens at any time. They can also do it after they've voted for a proposal. Thus, they are able to vote multiple times by just delegating to their own addresses.

## Impact
The whole governance system is broken because people have infinite votes.

## Code Snippet
After staking their tokens, the user calls [`castVote()`](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Governance.sol#L589-L646):
```sol
    function _castVote(address _voter, uint256 _proposalId, uint8 _support) internal returns (uint) {
        // Only Active proposals can be voted on
        if (state(_proposalId) != ProposalState.Active) revert InvalidStatus();
        
        // Only valid values for _support are 0 (against), 1 (for), and 2 (abstain)
        if (_support > 2) revert InvalidInput();

        Proposal storage proposal = proposals[_proposalId];

        // If the voter has already voted, revert        
        Receipt storage receipt = proposal.receipts[_voter];
        if (receipt.hasVoted) revert AlreadyVoted();

        // Calculate the number of votes a user is able to cast
        // This takes into account delegation and community voting power
        uint24 votes = (staking.getVotes(_voter)).toUint24();

        // Update the proposal's total voting records based on the votes
        if (_support == 0) {
            proposal.againstVotes = proposal.againstVotes + votes;
        } else if (_support == 1) {
            proposal.forVotes = proposal.forVotes + votes;
        } else if (_support == 2) {
            proposal.abstainVotes = proposal.abstainVotes + votes;
        }

        // Update the user's receipt for this proposal
        receipt.hasVoted = true;
        receipt.support = _support;
        receipt.votes = votes;

        // Make these updates after the vote so it doesn't impact voting power for this vote.
        ++totalCommunityScoreData.votes;

        // We can update the total community voting power with no check because if you can vote, 
        // it means you have votes so you haven't delegated.
        ++userCommunityScoreData[_voter].votes;

        return votes;
    }
```

The user's votes are determined by calling [`Staking.getVotes()`](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Staking.sol#L499-L501):

```sol
    function getVotes(address _account) public view returns (uint) {
        return tokenVotingPower[_account] + getCommunityVotingPower(_account);
    }
```

After that, the user calls [`delegate()`](https://github.com/sherlock-audit/2022-11-frankendao/blob/main/src/Staking.sol#L269-L316) to transfer their voting power:

```sol
  function _delegate(address _delegator, address _delegatee) internal lockedWhileVotesCast {
    address currentDelegate = getDelegate(_delegator);
    // If currentDelegate == _delegatee, then this function will not do anything
    if (currentDelegate == _delegatee) revert InvalidDelegation();

    // Set the _delegates mapping to the correct address, subbing in address(0) if they are delegating to themselves
    _delegates[_delegator] = _delegatee == _delegator ? address(0) : _delegatee;
    uint amount = votesFromOwnedTokens[_delegator];

    // If the delegator has no votes, then this function will not do anything
    // This is explicitly blocked to ensure that users without votes cannot abuse the refund mechanism
    if (amount == 0) revert InvalidDelegation();
    
    // Move the votes from the currentDelegate to the new delegatee
    // Neither of these addresses can be address(0) because: 
    // - currentDelegate calls getDelegate(), which replaces address(0) with the delegator's address
    // - delegatee is changed to msg.sender in the external functions if address(0) is passed
    tokenVotingPower[currentDelegate] -= amount;
    tokenVotingPower[_delegatee] += amount; 

    // If a user is delegating back to themselves, they regain their community voting power, so adjust totals up
    if (_delegator == _delegatee) {
      _updateTotalCommunityVotingPower(_delegator, true);

    // If a user delegates away their votes, they forfeit their community voting power, so adjust totals down
    } else if (currentDelegate == _delegator) {
      _updateTotalCommunityVotingPower(_delegator, false);
    }

    emit DelegateChanged(_delegator, currentDelegate, _delegatee);
  }
```

The `tokenVotingPower` for the current delegate is decreased and the one for the new delegate is increased. Thus, the new delegate can now also vote.

## Tool used

Manual Review

## Recommendation
Other protocols have solved the issue by locking up tokens after a vote.